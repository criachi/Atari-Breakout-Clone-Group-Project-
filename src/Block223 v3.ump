namespace ca.mcgill.ecse223.block.model;

use Block223PlayMode.ump;
use Block223Persistence.ump;
use Block223States.ump;

class Block223 {
   1 <@>- * User users;
   1 <@>- * UserRole roles;
   1 <@>- * Game games;
   
   public Game findGame(String name) {
	  for(Game game : getGames()) {
		  if (game.getName().equals(name)) {
			  return game;
		  }
	  }
	  return null;
  }
  
  public PlayedGame findPlayableGame(Integer id){
  	for(PlayedGame playedGame : getPlayedGames()) {
  		if ((playedGame.getId()) == id) {
  			return playedGame;
  		}
  	}
  	return null;
  }
  
  public String findUsername(UserRole userRole) {
  		String username = "";
  		for(User user : users) {
  			for(UserRole role : user.getRoles()) {
  				if(role == userRole) {
  					username += user.getUsername();
  					return username;
  				}
  			}
  		}
  		return username;
  }
  
}

class User {
   unique username;
   1 -> 1..2 UserRole roles;
   before constructor {
   	if(aUsername == null || aUsername == "") {
   		throw new RuntimeException("The username must be specified. ");
   	}
   }
   
   
   
}

class UserRole {
    before constructor {
   	if (aPassword == null || aPassword == "") {
   		throw new RuntimeException("The player password needs to be specified. ");
    }
   }
   abstract;
   password;
}

class Admin {
   isA UserRole;
}

class Player {
   isA UserRole;
}

class Game {
   const Integer MIN_NR_LEVELS = 1;
   const Integer MAX_NR_LEVELS = 99; // this is somewhat redundant because the max multiplicity is enforced by Umple
   const Integer PLAY_AREA_SIDE = 390; // play area is now constant
   const Integer WALL_PADDING = 10;
   const Integer COLUMNS_PADDING = 5;
   const Integer ROW_PADDING = 2;
   unique name;
   Integer nrBlocksPerLevel;
   before constructor, setName{
   	if(aName == "" || aName == null) {
   	throw new RuntimeException("The name of a game must be specified.");
   	}
   }
   before constructor, setNrBlocksPerLevel {
    if (aNrBlocksPerLevel <= 0) {
	  throw new RuntimeException("The number of blocks per level must be greater than zero.");
	}
   }
   before setNrBlocksPerLevel {
   	for(Level level: levels) {
   		if(aNrBlocksPerLevel < level.getBlockAssignments().size()) {
   			throw new RuntimeException("The maximum number of blocks per level cannot be less than the number of existing blocks in a level.");
   		}
   	}
   }
   * -- 1 Admin admin;
   1 <@>- * Block blocks;
   1 <@>- 1..99 Level levels;
   1 <@>- * BlockAssignment blockAssignments;
   1 <@>- 1 Ball ball;
   1 <@>- 1 Paddle paddle;

   public Block findBlock(int id) {
	List<Block> blocks = this.getBlocks();
	for(Block block: blocks) {
		if(id == block.getId()) {
			return block;
		}
	}
	return null; 
   }
   
     public Block getRandomBlock() {
	  int numOfBlocks = blocks.size();
	  Random rand = new Random();
	  
	  return blocks.get(rand.nextInt(numOfBlocks));
  }
}

class Block {
   before constructor {
   	if (aRed<0 || aRed>255) {
	  throw new RuntimeException("Red must be between 0 and 255.");
	}
	if (aGreen<0 || aGreen>255) {
	  throw new RuntimeException("Green must be between 0 and 255.");
	}
	if (aBlue<0 || aBlue>255) {
	  throw new RuntimeException("Blue must be between 0 and 255.");
	}
	if (aPoints<1 || aPoints>1000) {
	  throw new RuntimeException("Points must be between 1 and 1000.");
    }
   }
   before setRed {
    if(aRed<0 || aRed>255) {
    	throw new RuntimeException("Red must be between 0 and 255.");
    }
   }
   before setGreen {
   	if(aGreen<0 || aGreen>255) {
   		throw new RuntimeException("Green must be between 0 and 255.");
   	}
   }
   before setBlue {
    if(aBlue<0 || aBlue>255) {
    	throw new RuntimeException("Blue must be between 0 and 255.");
    }
   }
   before setPoints {
   	if (aPoints<1 || aPoints>1000) {
   		throw new RuntimeException("Points must be between 1 and 1000.");
   	}
   }
   const Integer MIN_COLOR = 0;
   const Integer MAX_COLOR = 255;
   const Integer MIN_POINTS = 1;
   const Integer MAX_POINTS = 1000; 
   const Integer SIZE = 20;
   autounique id;
   Integer red;
   Integer green;
   Integer blue;
   Integer points;
}

// random attribute not needed anymore
// Each level is filled up with random blocks just before playing the level to reach the nrBlocksPerLevel defined in Game
class Level {
	public BlockAssignment findBlockAssignment(int gridHorizontalPosition, int gridVerticalPosition) {
		List<BlockAssignment> blockAssignments = this.getBlockAssignments();
		for(BlockAssignment blockAssignment: blockAssignments) {
			int h = blockAssignment.getGridHorizontalPosition();
			int v = blockAssignment.getGridVerticalPosition();
				if(h == gridHorizontalPosition && v == gridVerticalPosition) {
					return blockAssignment;
				}
		}
		return null;
	}
}

class BlockAssignment {
   Integer gridHorizontalPosition;
   Integer gridVerticalPosition;
   * -- 1 Level level;
   * -- 1 Block block;
   before constructor{
   		if(aGridHorizontalPosition <= 0 || aGridHorizontalPosition > maxNrHorizontalBlocks()) {
   			throw new RuntimeException("The horizontal position must be between 1 and " + maxNrHorizontalBlocks() + ".");
   		}
   		if(aGridVerticalPosition <= 0 || aGridVerticalPosition > maxNrVerticalBlocks()) {
   			throw new RuntimeException("The vertical position must be between 1 and " + maxNrVerticalBlocks() + ".");
   		}
   }
   before setGridHorizontalPosition {
   		if(aGridHorizontalPosition <= 0 || aGridHorizontalPosition > maxNrHorizontalBlocks()) {
   			throw new RuntimeException("The horizontal position must be between 1 and " + maxNrHorizontalBlocks() + ".");
   		}
   }
   before setGridVerticalPosition {
   		if(aGridVerticalPosition <= 0 || aGridVerticalPosition > maxNrVerticalBlocks()) {
   			throw new RuntimeException("The vertical position must be between 1 and " + maxNrVerticalBlocks() + ".");
   		}
   }
   
   public static int maxNrHorizontalBlocks() {
		int max = (390 - 20 + 5)/25;
		return max;
	}
  
  public static int maxNrVerticalBlocks() {
	int max = (390 - 30 - 5 - 10 - 10 + 2) / 22;
	return max;
  }
  
}

class Ball {
   const Integer BALL_DIAMETER = 10;
   Integer minBallSpeedX;
   Integer minBallSpeedY;
   Double ballSpeedIncreaseFactor;
   before constructor, setMinBallSpeedX {
    if (aMinBallSpeedX < 0) {
	  throw new RuntimeException("The minimum speed of the ball must be greater than zero.");
	}
  }
   before constructor, setMinBallSpeedY {
    if (aMinBallSpeedY < 0) {
	  throw new RuntimeException("The minimum speed of the ball must be greater than zero.");
	}
  }
   before constructor, setBallSpeedIncreaseFactor {
    if (aBallSpeedIncreaseFactor <= 0) {
	  throw new RuntimeException("The speed increase factor of the ball must be greater than zero.");
	}
  }
}

class Paddle {
   const Integer PADDLE_WIDTH = 5;
   const Integer VERTICAL_DISTANCE = 30;
   Integer maxPaddleLength;
   Integer minPaddleLength;
    before constructor, setMaxPaddleLength {
    if (aMaxPaddleLength <= 0 || aMaxPaddleLength > 390) {
	  throw new RuntimeException("The maximum length of the paddle must be greater than zero and less than or equal to 390.");
	}
  }
    before constructor, setMinPaddleLength {
    if (aMinPaddleLength <= 0) {
	  throw new RuntimeException("The minimum length of the paddle must be greater than zero.");
	}
  }
}
