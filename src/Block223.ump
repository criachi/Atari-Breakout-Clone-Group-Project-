namespace model;

class User {
  autounique id;
  unique String name;
  1 -- 1 Player;
  1 -- 0..1 Admin;
}

class Player {
  String password;
  Integer currentLevel;
  1 -- * Game;
}

class Admin {
  String password;
  1 -- * Game;
}

class Game {
  unique String name;
  1 <@>- * Level;
  Integer playAreaSize;
  1 <@>- 1 HallOfFame;
  Double speedIncreaseFactor;
  Integer minBallSpeed;
  Integer maxBallSpeed;
  Integer minPaddleLength;
  Integer maxPaddleLength;
}

class HallOfFame {
  1 -- * User;
}

class Level {
  Integer levelNumber;
  1 <@>- * Block;
  1 <@>- 1 Ball;
  1 <@>- 1 Paddle;
}

class Ball {
  Integer speed;
  const DIAMETER = 10;
}

class Paddle {
  Integer length;
  const WIDTH = 5;
}

class Block {
  Integer size;
  Integer points;
  const SIDE = 20;
  enum Color { Blue, Red, Yellow, Green, Orange, Black, White };
  Color color;
  1 -- 1 GridCell;
}

class GridSystem {
  1 -- * GridCell;
  Integer gridLength;
  Integer gridWidth;
}

class GridCell {
  Integer row;
  Integer col;
}